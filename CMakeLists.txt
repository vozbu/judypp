CMAKE_MINIMUM_REQUIRED (VERSION 3.5)
PROJECT (judypp)

INCLUDE (CheckCXXSymbolExists)

SET (CMAKE_CXX_STANDARD 11)
SET (CMAKE_CXX_STANDARD_REQUIRED 11)

# Build flags
IF (CMAKE_CXX_COMPILER_ID STREQUAL "GNU"
 OR CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
    # Common options
    ADD_COMPILE_OPTIONS (-pipe)
    ADD_COMPILE_OPTIONS (-Wall)
    ADD_COMPILE_OPTIONS (-Wreorder)

#   Debug options to prevent buffer overflows (http://gcc.gnu.org/ml/gcc-patches/2004-09/msg02055.html)
#   This adds some extra checking code
    CHECK_CXX_SYMBOL_EXISTS (_FORTIFY_SOURCE "stddef.h" FORTIFY_SOURCE_IS_SET)
    IF (NOT FORTIFY_SOURCE_IS_SET)
        # If it is already set by default (like in Gentoo) we will get a lot of warnings about redifinition
        #ADD_COMPILE_DEFINITIONS (_FORTIFY_SOURCE=2)
        ADD_DEFINITIONS (-D_FORTIFY_SOURCE=2)
    ENDIF()

    ADD_COMPILE_OPTIONS (-fstack-protector)
ENDIF ()

#Packages
INCLUDE (FindPackageHandleStandardArgs)

## Judy
FIND_LIBRARY (LJUDY Judy)
FIND_PATH (IJUDY Judy.h)
SET (Judy_FIND_REQUIRED YES)
FIND_PACKAGE_HANDLE_STANDARD_ARGS (Judy DEFAULT_MSG LJUDY IJUDY)
ADD_LIBRARY (Judy::Judy UNKNOWN IMPORTED)
SET_TARGET_PROPERTIES (Judy::Judy PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES ${IJUDY}
    INTERFACE_COMPILE_DEFINITIONS JUDYERROR_NOTEST
    IMPORTED_LOCATION ${LJUDY})

## GoogleSparseHash
FIND_PATH (IGOOGLE_SPARSE_HASH sparsehash/dense_hash_set)
FIND_PACKAGE_HANDLE_STANDARD_ARGS (GOOGLE_SPARSE_HASH DEFAULT_MSG IGOOGLE_SPARSE_HASH)
IF (IGOOGLE_SPARSE_HASH)
    ADD_LIBRARY (GoogleSparseHash::GoogleSparseHash INTERFACE IMPORTED)
    SET_TARGET_PROPERTIES (GoogleSparseHash::GoogleSparseHash PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES ${IGOOGLE_SPARSE_HASH}
        INTERFACE_COMPILE_DEFINITIONS HAVE_GOOGLE_SPARSE_HASH)
ENDIF (IGOOGLE_SPARSE_HASH)

ADD_LIBRARY (judypp INTERFACE)
TARGET_INCLUDE_DIRECTORIES (judypp INTERFACE ${CMAKE_SOURCE_DIR}/include)
TARGET_LINK_LIBRARIES (judypp INTERFACE Judy::Judy)

FIND_PACKAGE (Boost 1.41.0 COMPONENTS unit_test_framework REQUIRED)
ENABLE_TESTING ()
ADD_SUBDIRECTORY (src/test)
